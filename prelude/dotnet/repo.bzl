load("@rules_dotnet//dotnet:defs.bzl", "nuget_repo")

def _nuget_packages_impl(module_ctx):
    """
    Implementation of the nuget_packages extension.

    Args:
        module_ctx: The module context
    """

    for module in module_ctx.modules:
        for from_file_tag in module.tags.from_file:
            nuget_deps_file = from_file_tag.nuget_deps
            
            # Load json file
            deps_string = module_ctx.read(nuget_deps_file)
            deps = json.decode(deps_string)   

            nuget_repo(
                name = from_file_tag.name,
                packages = deps
            )

_from_file_tag = tag_class(
    doc = """
    Imports Nuget packages from a nuget.deps.json file. Generated by the nuget2bazel
    tool.

    All direct and transitive dependencies of the NuGet packages are imported.
    """,
    attrs = {
        "name" : attr.string(
            doc = "Name of the NuGet package",
            mandatory = False,
            default = "nuget"
        ),
        "nuget_deps" : attr.label(
            doc = "Path to the generated nuget deps file",
            mandatory = True,
        )
    },
)

nuget_packages_extension = module_extension(
    implementation = _nuget_packages_impl,
    tag_classes = {
        "from_file": _from_file_tag,
    }
)